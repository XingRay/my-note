# Bazel 学习笔记 (二) C++ 基础构建规则

## Bazel 概念和术语

我们以[上一节](https://zhuanlan.zhihu.com/p/411563404)的案例为例，对 Bazel 中的概念和术语进行介绍。更为详细的介绍可见 [Bazel官网](https://docs.bazel.build/versions/main/build-ref.html)。

```text
stage1
├── main
│   ├── BUILD
│   └── hello-world.cc
└── WORKSPACE
```

### 工作空间 (Workspace)

工作空间是一个包含用于构建软件的源文件的目录。每一个工作空间都有一个名为 `WORKSPACE` 或者 `WORKSPACE.bazel` 的文件，这个包含 `WORKSPACE` 文件的目录为工作空间的根目录。
工作空间下其他包含 `WORKSPACE` 文件的目录将被视为另一个工作空间，会被排除在当前工作空间之外。

案例中目录 `stage1` 包含了 `WORKSPACE` 文件，因此该目录被视为 bazel 工作空间。

### 仓库 (Repositories)

代码被组织在仓库中。包含 `WORKSPACE` 文件的目录为仓库的根目录，称为 `@`；这个名称也可以在 `WORKSPACE` 文件中使用 `workspace` 规则进行定义，例如以下代码将当前仓库名称定义为 `@myrepo`：

```text
workspace(name = "myrepo")
```

案例中 `stage1` 及目录下所有的文件、子目录作为一个仓库。

### 包 (Packages)

包是仓库中代码的基础单元，包是有关的文件和其依赖清单的集合。包被定义在一个包含 `BUILD` 或 `BUILD.bazel` 文件的目录下，其包含目录下的所有文件以及子目录。 包中其他包含 `BUILD` 文件的目录会被排除在当前包之外。

案例中的 `main` 目录中包含 `BUILD` 文件，因此 `main` 目录下的所有文件和子目录构成了一个包。

### 目标 (Targets)

包中的元素被称为目标。目标通常有2种类型：文件 (files) 和规则 (rules)。 文件包括开发人员编写的源文件 (source files) 和构建工具根据规则从源文件生成的生成文件 (generated files / derived files / output files)； 规则说明了输入和输出的关系或从输入生成输出的步骤。

案例中的 `main/BUILD` 文件中通过 `cc_binary` 制定了一条从源文件 (输入) `hello-world.cc` 到可执行程序 (输出) `hello-world` 的规则。

### 标签 (Labels)

目标的名字被称为标签。标签通常具有以下形式：

```text
@myrepo//my/app/main:app_binary
```

第一部分 `@myrepo//` 为仓库的名称。当前仓库的可以简写为 `//`，项目主仓库名称为 `@//`。如果当前项目不是其他项目的外部依赖，当前仓库就是主仓库。
第二部分 `my/app/main` 为包相对根目录的位置，前两部分组合 `@myrepo//my/app/main` 构成了完整的包名。
第三部分 `app_binary` 为目标，这个目标可以是包中的文件，或者是定义在 `BUILD` 文件中的规则。

标签的简写规则：

- 当前仓库可以简写为 `//`
- 当前包中可以省略包名以及冒号，例如在 `@myrepo//my/app/main` 包中可以将 `@myrepo//my/app/main:app_binary` 省略简写为 `app_binary` 或 `:app_binary`
- 目标名称和其目录名称一致时可以省略目标名称以及冒号，例如可以将 `//my/app/lib:lib` 简写为 `//my/app/lib`

案例中 `BUILD` 文件中 `load` 函数的第一个参数就是一个标签：

```text
load("@rules_cc//cc:defs.bzl", "cc_binary")
```

标签 `@rules_cc//cc:defs.bzl` 仓库名称为 `@rules_cc`，包的位置为根目录下的 `cc` 目录，目标为 `defs.bzl` 文件。

在构建项目时也用到了标签：

```text
bazel build //main:hello-world
```

这里目标 `hello-world` 则是写在 `BUILD` 文件中的一条规则。

特别注意，甚至在 `cc_binary` 中也用到了标签，引用的源文件中 `hello-world.cc` 是标签 `//main:hello-world.cc` 的简写。

后续的案例中我们会看到更多关于标签的使用示例。

## Hello World 案例进阶

### 阶段二：单个包下多个目标构建

案例位于 `examples/cpp-tutorial/stage2`，其目录结构如下：

```text
stage2
├── main
│   ├── BUILD
│   ├── hello-world.cc
│   ├── hello-greet.cc
│   └── hello-greet.h
└── WORKSPACE
```

这里 `hello-greet` 作为 `hello-world` 的一项依赖。我们可以使用一条规则完成项目的构建：

```text
cc_binary(
    name = "hello-world",
    srcs = glob(["*.cc", "*.h"]),
)
```

这里 `glob` 负责找到当前目录下所有 `.cc` 文件和 `.h` 文件。虽然这种方式可以完成构建目标，但很显然并没有体现 `hello-greet` 是 `hello-world` 的依赖这一点，当包的规模增大后会造成混乱；更重要的是，`hello-greet` 作为一个库，应当能够在其他包中使用，如果使用上述规则而不将 `hello-greet` 作为一个独立的目标，则其他包想要使用这个库将无从下手。

因此，更优的方案是建立一条规则将 `hello-greet` 编译成一个库，然后在 `hello-world` 中将 `hello-greet` 作为依赖项，形成了案例中以下两条规则：

```text
cc_library(
    name = "hello-greet",
    srcs = ["hello-greet.cc"],
    hdrs = ["hello-greet.h"],
)

cc_binary(
    name = "hello-world",
    srcs = ["hello-world.cc"],
    deps = [
        ":hello-greet",
    ],
)
```

`cc_library` 规则用来构建库 (静态库或动态库)，在 `cc_binary` 中使用 `dep` 参数引用依赖库。
注意：`:hello-greet` 是标签 `//main:hello-greet` 的简略写法，因为 `hello-greet` 与 `hello-world` 在同一个包中；这里的冒号可以省略，即也可以简写为 `hello-greet`。

### 阶段三：多个包下多个目标构建

案例位于 `examples/cpp-tutorial/stage3`，其目录结构如下：

```text
stage3
├── main
│   ├── BUILD
│   ├── hello-world.cc
│   ├── hello-greet.cc
│   └── hello-greet.h
├── lib
│   ├── BUILD
│   ├── hello-time.cc
│   └── hello-time.h
└── WORKSPACE
```

该案例中有 `lib` 和 `main` 两个子目录，每个目录下都有一个 `BUILD` 文件，因此在工作区中包含 `lib` 和 `main` 两个包。目标 `hello-world` 依赖同一包中的 `hello-greet` 与 `lib` 包中的 `hello-time`。

在 `lib/BUILD` 中定义了如下规则：

```text
cc_library(
    name = "hello-time",
    srcs = ["hello-time.cc"],
    hdrs = ["hello-time.h"],
    visibility = ["//main:__pkg__"],
)
```

该规则构建一个名为 `hello-time` 的库，通过 `visibility` 参数声明了谁可以使用这个库，默认情况下 (即未添加 `visibility` 参数时) 仅在同一个 `BUILD` 文件中可以使用；这里 `//main:__pkg__` 表明 `main` 包可以使用这个库。

在 `main/BUILD` 中定义了如下规则：

```text
cc_library(
    name = "hello-greet",
    srcs = ["hello-greet.cc"],
    hdrs = ["hello-greet.h"],
)

cc_binary(
    name = "hello-world",
    srcs = ["hello-world.cc"],
    deps = [
        ":hello-greet",
        "//lib:hello-time",
    ],
)
```

仅有一点与 stage2 不同：`hello-world` 多了一个依赖项 `//lib:hello-time`。

特别注意 `hello-world.cc` 中包含 `hello-time` 库头文件的写法：

```text
#include "lib/hello-time.h"
```

头文件格式为：包位置/文件名。对于同一包中的头文件则可以省略包名，即 `#include "main/hello-greet.h"` 也可以写为 `#include "hello-greet.h"`。

例如，我们对案例目录结构稍作更改，在 `lib` 下新建 `hello-time` 目录用来存放 `hello-time` 库相关文件：

```text
stage3-1
├── main
│   ├── BUILD
│   ├── hello-world.cc
│   ├── hello-greet.cc
│   └── hello-greet.h
├── lib
│   └── hello-time
│       ├── BUILD
│       ├── hello-time.cc
│       └── hello-time.h
└── WORKSPACE
```

这时 `hello-time` 的标签将变为 `//lib/hello-time:hello-time` (或简写为 `//lib/hello-time`，因为目标名与目录同名)，因此需要对项目做出如下更改以构建目标：

- `lib/hello-time/hello-time.cc` 中头文件包含改为 `#include "lib/hello-time/hello-time.h"` 或 `#include "hello-time.h"`
- `main/BUILD` 中 `cc_binary` 规则的 `deps` 参数修改 `hello-time` 的标签为 `//lib/hello-time:hello-time` 或 `//lib/hello-time`
- `main/hello-world.cc` 中头文件包含改为 `#include "lib/hello-time/hello-time.h"`

## 构建规则基础

以上 C++ 案例中已经使用到了 `cc_binary` 与 `cc_library` 这两条规则，构建 C++ 项目时还有另外一些常用规则。以下将对这些规则以及参数进行介绍，更为详细的介绍可见 [Bazel官网](https://docs.bazel.build/versions/main/be/c-cpp.html)。

### cc_binary

规则 `cc_library` 用来构建可执行文件，其可用参数如下：

```text
cc_binary(name, deps, srcs, data, additional_linker_inputs, args, compatible_with, copts, defines, deprecation, distribs, env, exec_compatible_with, exec_properties, features, includes, licenses, linkopts, linkshared, linkstatic, local_defines, malloc, nocopts, output_licenses, restricted_to, stamp, tags, target_compatible_with, testonly, toolchains, visibility, win_def_file)
```

常用参数说明 (更为详细的说明可见 [Bazel官网](https://docs.bazel.build/versions/main/be/c-cpp.html%23cc_binary))：

- name：目标名称
- srcs：用以构建 C++ 目标所需要的文件列表 (包括头文件、源文件、编译中间文件)
- deps：需要链接到目标的库，通常是 `cc_library` 目标
- linkstatic：是否将依赖库静态编译到目标中

### cc_library

规则 `cc_library` 用来构建库，其可用参数如下：

```text
cc_library(name, deps, srcs, data, hdrs, alwayslink, compatible_with, copts, defines, deprecation, distribs, exec_compatible_with, exec_properties, features, implementation_deps, include_prefix, includes, licenses, linkopts, linkstamp, linkstatic, local_defines, nocopts, restricted_to, strip_include_prefix, tags, target_compatible_with, testonly, textual_hdrs, toolchains, visibility, win_def_file)
```

常用参数说明 (更为详细的说明可见 [Bazel官网](https://docs.bazel.build/versions/main/be/c-cpp.html%23cc_library))：

- name：目标名称
- srcs：用以构建 C++ 目标所需要的文件列表 (包括头文件、源文件、编译中间文件)
- hdrs：依赖该库的目标中可以直接包含的头文件列表

特别注意，习惯上 srcs 与 hdrs 中的文件是有区别的： 当 `cc_library` 规则构建的库作为另一规则的 `deps` 时，另一规则可以直接包含 (include) hdrs 中列举的头文件，而不能直接使用 srcs中列举的头文件。

例如在 stage3 案例中如果 `lib/BUILD` 中将 `hello-time.h` 放到了 `srcs` 列表中：

```text
cc_library(
    name = "hello-time",
    srcs = ["hello-time.cc", "hello-time.h"],
    visibility = ["//main:__pkg__"],
)
```

则依赖 `hello-time` 库的目标中文件不能直接包含 `hello-time.h` 这一文件，即 `hello-world.cc` 中不应出现 `#include "lib/hello-time.h"` 类似语句。但很不幸目前 Bazel 并不能检测到这一错误，即使在 `hello-world.cc` 使用了 `hello-time.h` 头文件也依然能够成功构建项目。

### cc_import

规则 `cc_import` 用来导入预先编译的库 (静态库，共享库，接口库等)，其可用参数如下：

```text
cc_import(name, data, hdrs, alwayslink, compatible_with, deprecation, distribs, features, interface_library, licenses, restricted_to, shared_library, static_library, system_provided, tags, target_compatible_with, testonly, visibility)
```

将预先编译的库使用 `cc_import` 导入后，其他规则可以在 `deps` 中像依赖 `cc_library` 规则一样依赖预先编译的库。例如有静态库 `libmylib.a`，共享库 `libmylib.so`，在目标 `first` 中需要链接静态库 `libmylib.a`，在目标 `second` 中需要链接动态库 `libmylib.so`，则可以制定以下规则：

```text
cc_import(
  name = "mylib",
  hdrs = ["mylib.h"],
  static_library = "libmylib.a",
  shared_library = "libmylib.so",
)

cc_binary(
  name = "first",
  srcs = ["first.cc"],
  deps = [":mylib"],
  linkstatic = 1, # default value
)

cc_binary(
  name = "second",
  srcs = ["second.cc"],
  deps = [":mylib"],
  linkstatic = 0,
)
```

参数 `linkstatic` 来指定使用静态库还是共享库进行目标构建，更为详细的说明可见 [Bazel官网](https://docs.bazel.build/versions/main/be/c-cpp.html%23cc_import)。

[下一节](https://zhuanlan.zhihu.com/p/415586721)中会介绍如何依赖外部仓库，包括本地仓库与远程仓库。