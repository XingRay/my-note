# Bazel 学习笔记 (三) 依赖外部仓库

[上一节](https://zhuanlan.zhihu.com/p/412355447)主要讨论了多目标构建，示例中所有的源代码都在同一个仓库下。我们在实际开发项目时常常需要依赖第三方库，但通常不希望将库的所有源代码直接复制到我们的项目仓库中。因此需要讨论如何让我们的项目依赖于外部仓库。本节以依赖 `Eigen3` 仓库为例，讨论如何依赖本地仓库与远程仓库。

`Eigen` 是线性代数的 `C++` 模板库，他提供了矩阵、向量、数值求解等相关算法。关于 `Eigen` 的详细介绍可参考其[官网](https://eigen.tuxfamily.org/)。

我们将尝试使用 `Bazel` 来构建一个 `Eigen3` 的示例程序：

```cpp
#include <iostream>
#include "Eigen/Dense"

int main()
{
    Eigen::MatrixXd m = Eigen::MatrixXd::Random(3,3);
    m = (m + Eigen::MatrixXd::Constant(3,3,1.2)) * 50;
    std::cout << "m =" << std::endl << m << std::endl;
    Eigen::VectorXd v(3);
    v << 1, 2, 3;
    std::cout << "m * v =" << std::endl << m * v << std::endl;
}
```

程序首先随机生成一个3×3的矩阵，并与一个常数矩阵相加输出结果；其次计算矩阵和一个向量的乘积并输出结果。以下我们将分为3个阶段构建项目。

## 准备工作

- 下载 `Eigen3` 源代码 (例如 [eigen-3.4.0.tar.gz](https://gitlab.com/libeigen/eigen/-/archive/3.4.0/eigen-3.4.0.tar.gz)) 并解压 (例如解压到 `/home/username/eigen3`)
- 建立以下目录结构，并将上述示例程序复制到 `main.cc` 中

```text
stage0
├── main
│   ├── BUILD
│   └── main.cc
└── WORKSPACE
```

## 构建目标

### 阶段0：直接向编译器传递参数

由于使用 `Eigen` 编译程序只需要 `Eigen` 文件夹下的头文件，即只要编译器能够找到这些头文件。因此我们只需要向编译器传递 `-I` 参数即可实现项目构建。假设将 `Eigen3` 源代码解压到了 `/home/username/eigen3` 目录下，则在 `stage0/main` 路径下运行以下命令可以成功构建项目：

```bash
g++ -I /home/username/eigen3 main.cc -o main
```

如果已经将 `Eigen` 复制或符号链接到系统的包含文件目录 (通常是 `/usr/local/include/`) 下，则可免去添加 `-I` 参数。

在 `cc_binary` 规则中，使用 `copts` 将参数添加到 C++ 编译命令中 (或者使用 `includes` 参数)。假设我们编写 `main/BUILD` 文件如下：

```python
cc_binary(
    name = "main",
    srcs = ["main.cc"],
    copts = ["-I /home/username/eigen3"],
)
```

并用以下命令去构建项目：

```bash
bazel build //main
```

通常会收到类似以下的错误：

```bash
ERROR: /home/username/stage0/main/BUILD:1:10: undeclared inclusion(s) in rule '//main:main':
this rule is missing dependency declarations for the following files included by 'main/main.cc':
```

这是因为 Bazel 规定目标用到的头文件必须添加到 `srcs` 或 `hdrs` 参数中，对此相关[说明](https://docs.bazel.build/versions/main/be/c-cpp.html%23cc_binary.includes) (令人疑惑的是在 Windows 10 上我使用 Bazel 3.7.2 竟然能够成功编译，似乎并没有对用到的头文件进行检查，类似的问题 issues [#9965](https://github.com/bazelbuild/bazel/issues/9965))。然而如果需要将所用到的头文件全部列出，必然会造成混乱。因此，直接向编译器传递参数的方式并不合理，似乎也不可行。

### 阶段1：依赖本地仓库

我们尝试将 `Eigen3` 源代码作为一个本地外部仓库，我们的项目将依赖这个仓库。实现这一点需要使用[工作空间规则](https://docs.bazel.build/versions/main/be/workspace.html)。`Eigen3` 并不是一个 Bazel仓库，因此使用 `new_local_repository` 规则 (如果外部依赖是一个 Bazel 仓库，则使用 `local_repository` 规则)。

修改目录结构如下：

```text
stage1
├── main
│   ├── BUILD
│   └── main.cc
├── eigen3
│   └── BUILD
└── WORKSPACE
```

在 `eigen3/BUILD` 文件中建立以下构建规则：

```python
EIGEN3_HEADERS = glob([
    "Eigen/*",
])
EIGEN3_SOURCES = glob([
    "Eigen/**/src/**/*.h",
])

cc_library(
    name = "eigen3",
    hdrs = EIGEN3_HEADERS,
    srcs = EIGEN3_SOURCES,
    visibility = ["//visibility:public"],
)
```

函数 `glob` 可以帮助我们匹配满足路径格式的文件，其详细使用方法与路径格式的书写规则可见[官方文档](https://docs.bazel.build/versions/main/be/functions.html%23glob)；`cc_library` 中参数 `visibility = ["//visibility:public"]` 表明该库对所有包可见。

在 `WORKSPACE` 文件建立规则中引入外部仓库 (假设 `Eigen3` 源代码解压到了 `/home/username/eigen3` 目录)：

```python
new_local_repository(
    name = "eigen3",
    path = "/home/username/eigen3",
    build_file = "eigen3/BUILD",
)
```

参数 `build_file` 是相对于主仓库的路径或绝对路径，关于 `new_local_repository` 详细使用方法与参数说明可见[官方文档](https://docs.bazel.build/versions/main/be/workspace.html%23new_local_repository)。

在 `main/BUILD` 文件中建立以下构建规则：

```python
cc_binary(
    name = "main",
    srcs = ["main.cc"],
    deps = ["@eigen3//:eigen3"],
)
```

注意标签 `@eigen3//:eigen3` 中 `//` 后直接加 `:` 是 `build_file` 参数指定文件中的规则。

在根目录 `stage1` 下使用以下命令构建项目：

```bash
bazel build //main
```

### 阶段2：依赖远程仓库

建立并依赖本地仓库的方法看上去不错，能够成功构建出项目。但是，如果我们需要将项目迁移到另一台主机时，所有的外部依赖都需要重新手动安装，仓库的路径需要重新配置，这非常繁琐；更糟糕的是，如果重新安装的外部依赖版本与原来的不一致，很有可能会因版本兼容性问题而构建失败。因此，需要讨论如何让 `Bazel` 自动去获取特定版本的远程仓库。

可以使用 `http_archive` 函数实现远程仓库的获取，注意该函数需要事先通过 `load` 导入。在阶段1的基础上仅需对 `WORKSPACE` 文件进行修改即可实现我们的目标 (这里参考了 [TensorFlow](https://github.com/tensorflow/tensorflow/blob/v2.6.0/third_party/eigen3/workspace.bzl) 源代码)：

```python
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

EIGEN_COMMIT = "12e8d57108c50d8a63605c6eb0144c838c128337"
EIGEN_SHA256 = "f689246e342c3955af48d26ce74ac34d21b579a00675c341721a735937919b02"

http_archive(
    name = "eigen3",
    build_file = "//eigen3:BUILD",
    sha256 = EIGEN_SHA256,
    strip_prefix = "eigen-{commit}".format(commit = EIGEN_COMMIT),
    urls = [
        "https://storage.googleapis.com/mirror.tensorflow.org/gitlab.com/libeigen/eigen/-/archive/{commit}/eigen-{commit}.tar.gz".format(commit = EIGEN_COMMIT),
        "https://gitlab.com/libeigen/eigen/-/archive/{commit}/eigen-{commit}.tar.gz".format(commit = EIGEN_COMMIT),
    ],
)
```

这样可以在构建目标时会自动去下载 `url` 参数指定的文件并解压，确保项目迁移时依赖环境版本不会出错。

对于大型项目，可能会有较多的外部依赖，通常会将外部依赖放到 `third_party` 目录下，项目目录结构大致为：

```text
app
├── main
│   └── # 省略部分文件
├── third_party
│   ├── eigen
│   │   └── # 省略部分文件
│   └── # 省略部分依赖目录
└── WORKSPACE
```

## 测试结果

构建生成的可执行文件在 `bazel-bin/main` 目录下，尝试运行得到以下输出结果 (具体结果是由生成的随机矩阵决定的)：

```bash
$ ./main
m =
94.0188  89.844 43.5223
49.4383 101.165  86.823
88.3099 29.7551 37.7775
m * v =
404.274
512.237
261.153
```

`Bazel` 提供了很多常用的规则，例如我们已经使用过的 `cc_binary` `cc_library` `new_local_repository`等。有时候这些规则并不能满足我们的需求，在[下一节](https://zhuanlan.zhihu.com/p/421489117)中将讨论自定义规则的创建。