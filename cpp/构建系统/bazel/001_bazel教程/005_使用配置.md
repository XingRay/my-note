# Bazel 学习笔记 (五) 使用配置

[上一节](https://zhuanlan.zhihu.com/p/421489117)中讨论了宏与规则的创建，本节将讨论如何根据用户配置进行构建。

在构建项目时，有时需要根据用户配置 (例如 CPU 平台，编译选项等) 构建出不同的结果。用户通过命令行或者配置文件设置相关配置，我们需要编写可配置的规则使 Bazel 能够根据这些配置进行目标构建。

## 通过命令行传递配置

命令行以 `--flag=xxx` 的形式传递配置参数，可以利用 `rule` 函数中的 `build_setting` 参数实现这一功能，在规则的 `implementation` 函数中使用 `ctx.build_setting_value` 进行读取。下面通过一个例子进行演示。

目录结构如下 (示例的逻辑相对简单，为了目录结构的简洁，不再将相关规则单独写到一个包中，而是直接放在工作空间的根目录下，在实际项目中请不要这样做)：

```text
config-example
├── BUILD
├── defs.bzl
└── WORKSPACE
```

在 `defs.bzl` 文件中定义一条能够进行配置天气的规则，在其 `implementation` 函数中打印出设置的天气：

```python
# defs.bzl
def _weather_impl(ctx):
    weather = ctx.build_setting_value
    print("It's {0} today!".format(weather))

weather_flag = rule(
    implementation = _weather_impl,
    build_setting = config.string(flag = True),
)
```

`build_setting` 中可以指定配置的类型为：布尔类型 `config.bool`，整数类型 `config.int`，字符串类型 `config.string`，字符串列表类型 `config.string_list` 等，具体可见[官方文档](https://docs.bazel.build/versions/main/skylark/lib/config.html)。

设置这些配置参数类型时传递的 `flag` 参数标识能否通过命令行来设置这些参数，其默认值为 `False`。对于一些调试模式中的规则，如果不希望用户通过命令行进行设置，而是希望在内部使用默认值，则不必将 `flag` 设置为 `True`。另外对于字符串类型还可以设置 `allow_multiple` 参数来允许多次设置 (需要 Bazel 版本高于 5.0.0)，具体可见[官方文档](https://docs.bazel.build/versions/main/skylark/config.html%23defining-multi-set-string-flags)。

在 `BUILD` 文件中加载并实例化规则：

```python
# BUILD
load(":defs.bzl", "weather_flag")

weather_flag(
    name = "weather",
    build_setting_default = "sunny",
)
```

对于可以配置参数的规则 (定义规则时指定了 `build_setting` 参数的规则)，可以在实例化时通过 `build_setting_default` 参数指定配置的默认值。例如直接构建 `weather` 目标将得到类似以下输出：

```text
$ bazel build //:weather
DEBUG: /.../config-example/defs.bzl:3:10: It's sunny today!
```

注意在 `//` 后直接加 `:` 表示工作空间根目录下的目标，这里即 `BUILD` 文件中定义的相关规则。如果当前命令行路径已经位于工作空间根目录 (与 `BUILD` 文件在同一路径下)，则标签可进行简写，此时 `bazel build weather` 或 `bazel build :weather` 也可以构建目标。

可以通过命令行指定配置参数，例如设置天气为雨天，构建目标得到类似以下输出：

```text
$ bazel build //:weather --//:weather=rainy
DEBUG: /.../config-example/defs.bzl:3:10: It's rainy today!
```

在命令行中通过 `--name=value` 的形式设置参数，`name` 是可以配置参数的规则的标签，这里是 `//:weather`。注意 `build` 目标的标签可以简写，但是配置参数中的标签不可直接简写。如果当前路径位于工作空间根目录下，以下标签简写方式可以成功构建目标：

```bash
bazel build weather --//:weather=rainy
```

而以下命令则会报错：

```bash
bazel build weather --weather=rainy # not allowed
```

如何简写配置参数的标签将在后文进行介绍。

### 配置参数合法性检查

上述示例中，假设我们希望天气是晴天、阴天、雨天、雪天之一，即希望定义枚举类型的配置参数，则需要对参数进行检查。修改 `defs.bzl` 文件中的规则执行函数如下：

```python
# defs.bzl
weathers = ["sunny", "cloudy", "rainy", "snowy"]

def _weather_impl(ctx):
    raw_weather = ctx.build_setting_value
    if raw_weather not in weathers:
        fail("weather was set to unallowed value " + raw_weather)
    print("It's {0} today!".format(raw_weather))

weather_flag = rule(
    implementation = _weather_impl,
    build_setting = config.string(flag = True),
)
```

此时如果指定不合法的天气进行构建会报错，例如：

```text
$ bazel build //:weather --//:weather=stormy
Error in fail: weather was set to unallowed value stormy
```

### 配置参数简写 (Bazel 版本高于 4.0.0)

注意：使用这项特性需要 Bazel 版本高于 4.0.0，可以使用 `bazel version` 命令检查当前 `Bazel` 版本。

使用命令行传递配置参数时，标签需要书写为全名而不可直接简写。如果某一项可配置的规则位于多级子目录下，其标签格式可能为 `//dir1/dir2/.../pkg:target`，每次输入构建命令时都需要输入标签全名十分不便，且不易阅读。可以使用给配置参数设置别名的方法来简化参数的设置。

在工作空间的根目录下 (与 `WORKSPACE` 文件同级) 新建 `.bazelrc` 文件 (注意这个文件名不可更改，且必须创建在工作空间根目录下，否则 Bazel 不会自动读取文件中的相关配置)，即目录结构变更如下：

```text
config-example
├── BUILD
├── defs.bzl
├── .bazelrc
└── WORKSPACE
```

在 `.bazelrc` 文件中使用 `build --flag_alias=ALIAS_NAME=TARGET_PATH` 的格式设置别名，其中 `ALIAS_NAME` 部分是指别名 (简写名称)，`TARGET_PATH` 部分是指标签，其余部分固定格式。例如在 `.bazelrc` 设置 `//:weather` 的别名为 `weather`：

```text
# .bazelrc
build --flag_alias=weather=//:weather
```

则可以使用以下命令构建目标：

```bash
bazel build //:weather --weather=rainy
```

### 从文件导入配置参数

`.bazelrc` 文件中除了可以设置别名，还可以直接设置配置参数。例如在文件中设置天气为阴天：

```text
# .bazelrc
build --//:weather=cloudy
```

构建目标将得到类似以下输出：

```text
$ bazel build //:weather
DEBUG: /.../config-example/defs.bzl:7:10: It's cloudy today!
```

因此除了通过命令行直接传递配置参数外，还可以将配置写在 `.bazelrc` 文件中。注意如果在命令行与 `.bazelrc` 文件中同时配置了同一项参数，则命令行配置的优先级更高。

### 分离用户配置

考虑如下需求：项目的一部分配置是关于系统环境的，需要在 `.bazelrc` 文件中直接设置，且不希望用户随意更改；另外一些配置需要用户手动设置，或者通过脚本生成 (例如 TensorFlow 项目通过让用户运行 `configure.py` 脚本生成相关配置文件)。这种情况下最好能够将用户配置分离出 `.bazelrc` 文件，这可以在 `.bazelrc` 文件中引入其他配置文件实现。

在工作空间下新建用户配置文件 `.bazelrc.user` (文件名可以任取)，并在 `.bazelrc` 中使用 `try-import` 引入 (使用 `try-import` 即使文件不存在也不会出错)：

```python
# .bazelrc
# ... 省略相关配置
# 引入用户配置
try-import %workspace%/.bazelrc.user
```

这样可以在用户配置文件中进行参数设定，例如将天气设置为雪天：

```python
# .bazelrc.user
build --//:weather=snowy
```

特别注意 `.bazelrc` 文件中的配置与引入用户文件的顺序。如果没有允许多次设置，后面的配置会覆盖前面的配置。假设在 `.bazelrc.user` 中设置天气为雪天，以下引入顺序能够设置天气为雪天：

```python
# .bazelrc
build --//:weather=cloudy
try-import %workspace%/.bazelrc.user
```

而以下引入顺序天气会被覆盖为阴天：

```python
# .bazelrc
try-import %workspace%/.bazelrc.user
build --//:weather=cloudy
```

### 使用预定义配置规则

[Skylib](https://github.com/bazelbuild/bazel-skylib) 库为我们提供了一些预定义配置，例如 `string_flag` 等 ([完整列表](https://github.com/bazelbuild/bazel-skylib/blob/main/rules/common_settings.bzl))，我们可以直接引入并实例化这些配置规则而不需要自己定义。

```python
load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
string_flag(
    name = "myflag",
    values = ["a", "b", "c"],
    build_setting_default = "a",
)
```

注意使用 Skylib 需要在 `WORKSPACE` 文件中添加外部依赖，具体[设置说明](https://github.com/bazelbuild/bazel-skylib/releases)。

## 配置的依赖 (实现一条规则使用多个配置)

定义可配置的规则时，一条规则只能指定一个配置参数。对于上述示例，我们只能配置一个参数天气；如果我们希望能够同时配置天气与气温，并在一条规则中输出，则可以使用依赖的方式实现，利用 `Provider` 传递相关配置参数。示例的目录结构如下：

```text
config-dep-example
├── BUILD
├── defs.bzl
└── WORKSPACE
```

在 `defs.bzl` 文件中定义可配置的天气规则、温度规则以及输出配置的 "天气预报" 规则：

```python
# defs.bzl
WeatherInfo = provider(fields = ["weather"])
TemperatureInfo = provider(fields = ["temperature"])

def _weather_impl(ctx):
    return WeatherInfo(weather = ctx.build_setting_value)

def _temperature_impl(ctx):
    return TemperatureInfo(temperature = ctx.build_setting_value)

weather_flag = rule(
    implementation = _weather_impl,
    build_setting = config.string(flag = True),
)

temperature_flag = rule(
    implementation = _temperature_impl,
    build_setting = config.int(flag = True),
)

def _impl(ctx):
    forecast_dict = {
        "weather": ctx.attr.weather[WeatherInfo].weather,
        "temperature": ctx.attr.temperature[TemperatureInfo].temperature,
    }
    forecast_tmpl = \
"""
--------------- Weather Forecast ---------------
| Weather: {weather}    Temperature: {temperature} C
------------------------------------------------
"""
    print(forecast_tmpl.format(**forecast_dict))


forecast = rule(
    implementation = _impl,
    attrs = {
        "weather": attr.label(),
        "temperature": attr.label(),
    },
)
```

在 `BUILD` 文件中分别实例化天气规则、温度规则以及 "天气预报" 规则：

```python
# BUILD
load("defs.bzl", "weather_flag", "temperature_flag", "forecast")

weather_flag(name = "weather", build_setting_default="sunny")
temperature_flag(name = "temperature", build_setting_default=0)

forecast(
    name = "forecast",
    weather = ":weather",
    temperature = ":temperature",
)
```

使用如下命令构建项目并配置天气、温度参数，得到类似以下输出：

```text
$ bazel build //:forecast --//:weather=rainy --//:temperature=10
DEBUG: /.../config-dep-example/defs.bzl:31:10: 
--------------- Weather Forecast ---------------
| Weather: rainy    Temperature: 10 C
------------------------------------------------
```

### Provider 说明

上述示例中使用 `Provider` 传递配置参数，下面对 `Provider` 的使用进行简单说明。

### 定义与实例化

- 使用 `provider` 函数定义，函数原型

```python
Provider provider(doc='', *, fields=None)
```

- 参数 `fields` 接受一个字符串列表或是包含说明文档的字典 (默认为 `None`)，例如：

```python
provider(fields = ['a', 'b'])
provider(fields = { 'a' : 'Doc for a', 'b' : 'Doc for b' })
```

- 实例化 `Provider` 时需要设定 `fields` 参数中指定字段的值 (如果设定了 `fields` 参数以外的字段会报错；若 `fields` 为 `None` 则可以任意设置字段)：

```python
Provider1 = provider(fields = ['a', 'b'])
Provider2 = provider()

p1 = Provider1(a = 1, b = 2)
p1_1 = Provider1(a = 1) # 后续如果访问 b 字段报错
# p1_2 = Provider(a = 1, b = 2, c = 3) # 错误，字段 c 不存在

p2 = Provider2(x = 1, y = 2, z = 3) # 可以任意设置字段
```

- 通过 `.` 访问 `Provider` 实例中各字段的值，例如：

```python
print("{0} + {1} = {2}".format(p1.a, p1.b, p1.a + p1.b))
```

### 使用场景

规则的 `implementation` 函数可以返回一个 `Provider` 或者一个 `Provider` 列表，用来传递信息。例如上述示例中将天气配置设置到 `Provider` 实例对应字段并返回：

```python
WeatherInfo = provider(fields = ["weather"])

def _weather_impl(ctx):
    return WeatherInfo(weather = ctx.build_setting_value)

weather_flag = rule(
    implementation = _weather_impl,
    build_setting = config.string(flag = True),
)
```

通过标签 (或标签列表) 类型的属性定义能够依赖其他目标的规则 (这个属性并不一定要命名为 `dep` 或 `deps`，可以根据需要灵活命名)，例如上述示例中 `forecast` 规则定义了两个标签属性名称为 `weather` 与 `temperature`：

```python
forecast = rule(
    implementation = _impl,
    attrs = {
        "weather": attr.label(),
        "temperature": attr.label(),
    },
)
```

实例化 `forecast` 规则时可以使用 `weather` 参数与 `temperature` 参数分别设定依赖的规则：

```python
forecast(
    name = "forecast",
    weather = ":weather",
    temperature = ":temperature",
)
```

如此可以在规则的 `implementation` 函数中使用 `ctx.attr.attr_name[provider_name]` 的方式获取到依赖项返回的 `Provider`，例如 `forecast` 规则的 `implementation` 函数中分别获取了 `weather_flag` 与 `temperature_flag` 规则返回的 `Provider`：

```python
ctx.attr.weather[WeatherInfo].weather
ctx.attr.temperature[TemperatureInfo].temperature
```

如果 `implementation` 函数返回的是 `Provider` 列表，则可以根据 `Provider` 的名称分别访问，例如：

```python
Info1 = provider(fields = ["key"])
Info2 = provider(fields = ["key"])

def _parent_impl(ctx):
    # ... 省略部分代码
    return [Info1(key = value1), Info2(key = value2)]

def _impl(ctx):
    value1 = ctx.attr.attr_name[Info1].key
    value2 = ctx.attr.attr_name[Info2].key
```

