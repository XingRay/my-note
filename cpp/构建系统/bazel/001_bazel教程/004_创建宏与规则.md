# Bazel 学习笔记 (四) 创建宏与规则

[上一节](https://zhuanlan.zhihu.com/p/415586721)讨论了对外部仓库的依赖。本节将对 Bazel 的规则展开讨论，并以尝试创建自定义规则。

## 相关概念

### 宏 (Macros) 与规则 (Rules)

- 宏：实例化规则的函数。当我们需要在 `BUILD` 文件中重复使用一些功能时可以定义宏。
- 规则：规则定义了 Bazel 从输入构建输出的一系列动作，规则比宏的功能更加强大，能够控制 Bazel 内部的整个构建执行流程。

如果仅需要重复使用简单的逻辑，通常创建宏；而逻辑复杂时创建规则会更优。宏和规则作为 Bazel 的扩展部分，需要定义在后缀为 `.bzl` 的文件中，使用 `Starlark` (类似`Python3`) 语言书写 ([语法参考](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/skylark/language.html%23syntax)，[API 文档](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/skylark/lib/skylark-overview.html))。

### Bazel 构建阶段

Bazel 的构建过程分为3个阶段：

- 加载 (Loading)：加载构建目标所需要的所有 `.bzl` 等扩展文件 和 `BUILD` 文件，执行 `BUILD` 文件实例化规则 (宏在这一阶段执行)，并将规则添加到一张图中。
- 分析 (Analysis)：执行规则的 `implementation` 函数实例化行动 (actions，描述了如何从一系列输入得到一系列输出，并明确地列出输出的文件，例如：run gcc on hello.c and get hello.o)。根据加载阶段得到的规则图创建出行动图。
- 执行 (Execution)：运行命令以执行 actions。如果输出文件与 actions 中规定的不一致或者命令运行出错，则构建失败。

Bazel 通过对依赖进行分析能够知道构建当前目标，哪些文件必须加载，哪些规则必须分析，哪些行动必须执行。

特别注意规则和宏的定义与实例化。我们在 `.bzl` 文件中定义宏与规则，而在 `BUILD` 文件执行中调用这些宏与规则时才是实例化的阶段。

## 创建宏

下面通过一个创建文件的例子来演示如何通过定义宏来重复使用逻辑。我们通常可以使用 `echo` `cp` 等命令来创建文件并写入内容，例如以下命令将 "Hello World!" 写入文件 `hello.txt` 中 (当文件不存在时会新建文件)：

```bash
echo Hello World! > hello.txt
```

在 Bazel 中，可以使用 `genrule` 来运行命令生成文件。考虑如下目录结构：

```text
macro-example
├── generate_files
│   └── BUILD
└── WORKSPACE
```

在 `generate_files/BUILD` 文件中定义如下规则：

```python
genrule(
    name = "hello",
    outs = ["hello.txt"],
    cmd = "echo Hello World! > $@",
)
```

其中 `$@` 指代 `outs` 参数中的文件，这是在 `genrule` 中可以使用的特殊变量，后文将会对此进行介绍。

运行以下命令构建目标，在 `bazel-bin/generate_files/` 目录下生成了 `hello.txt` 文件：

```bash
bazel build //generate_files:hello
```

如果只是简单的创建一个文件，使用上述方法没有问题。当我们需要创建很多个文件，并需要在创建文件时进行额外的操作 (例如我们想在写入的内容前加一句问候语)，不妨定义宏。首先在 `generate_files/` 目录下创建一个后缀为 `.bzl` 的文件，例如 `generator.bzl`：

```text
macro-example
├── generate_files
│   ├── BUILD
│   └── generator.bzl
└── WORKSPACE
```

在 `generate_files/generator.bzl` 文件中定义宏：

```python
def file_generator(name, content):
    content = "Hello %s!\n" % name + content
    native.genrule(
        name = name + "_gen",
        outs = [name + ".txt"],
        cmd = "echo \"%s\" > $@" % content,
    )
```

通过 `native` 来调用 Bazel 的原生规则；由于写入的内容中包含换行符，因此需要使用引号防止被解释为两条命令。

在 `generate_files/BUILD` 文件中加载宏并使用宏定义如下规则：

```python
load("//generate_files:generator.bzl", "file_generator")

file_generator(
    name = "Alex",
    content = "Love look not with eyes, but with the mind.",
)

file_generator(
    name = "Bob",
    content = "Sweet are the uses of adversity.",
)
```

运行以下命令可以创建文件并写入内容 (注意这里的目标名称，宏被转换为规则后其目标名称是实际传入 `genrule` 的 `name` 参数)：

```bash
bazel build //generate_files:Alex_gen
```

### genrule 函数简介

`genrule` 通过运行命令来创建一个或多个文件，其完整说明可见 [官方文档](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/be/general.html%23genrule)。

其常用参数举例说明如下：

- `name`：规则名称，可以在其他规则中引用此标签作为依赖
- `srcs`：输入文件的标签列表，执行的命令如果有输入文件，则应在此处列举出这些文件
- `outs`：运行命令后应该生成文件的标签列表
- `cmd`：需要执行的命令
- `tools`：命令调用的目标程序 (通常由*_binary规则构建出) 标签列表，Bazel 会在调用命令前首先构建出这些目标

在 `genrule` 中可以使用预定义变量，运行以下命令可以查看当前工作空间中的预定义变量：

```bash
bazel info --show_make_env
```

使用 `$` 来引用这些变量，例如使用 `$(TARGET_CPU)` 来引用目标架构的 CPU。`genrule` 中还可以使用以下特殊的预定义变量：

- `OUTS`：`genrule` 的 `outs` 参数中的文件列表，如果只有一个文件则可以使用 `@` 代替
- `SRCS`：`genrule` 的 `srcs` 参数中的文件列表，如果只有一个文件则可以使用 `<` 代替

可以使用预定义的 `execpath`, `rootpath`, `location` 来获取源文件或者生成目标的路径，这些变量需要接受一个标签作为参数并得到标签对应文件的路径。

关于预定义变量的完整介绍可见 [官方文档](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/be/make-variables.html)，下面将通过示例来具体说明 `genrule` 的使用。

### genrule 示例

示例首先从 C++ 源代码构建一个来回显参数的程序 `myecho`，然后使用这个程序来创建文件。目录结构如下：

```text
genrule-example
├── generate_files
│   └── BUILD
├── myecho
│   ├── BUILD
│   └── myecho.cc
└── WORKSPACE
```

文件 `myechoe/myecho.cc` 中通过以下代码来模拟 `echo` 命令：

```cpp
#include <iostream>

int main(int argc, char * argv[])
{
    while(--argc)
        std::cout << *++argv << (argc > 1? " ": "");
    std::cout << std::endl;
}
```

并在文件 `myecho/BUILD` 中定义如下规则生成目标程序：

```python
cc_binary(
    name = "myecho",
    srcs = ["myecho.cc"],
    visibility = ["//visibility:public"]
)
```

在文件 `generate_files/BUILD` 中定义规则，通过调用生成的 `myecho` 程序来创建文件：

```python
genrule(
    name = "hello",
    outs = ["hello.txt"],
    cmd = "$(location //myecho) Hello World! > $@",
    tools = ["//myecho"],
)
```

注意这里使用 `location` 来引用目标 `//myecho:myecho` 生成的程序 `myecho` 的路径，`$@` 指代参数 `outs` 列表中的 `hello.txt` 文件。如果不考虑路径， `genrule` 最终运行了命令 `myecho Hello World! > hello.txt`，而预定义变量则帮助我们补充完整了路径。

运行以下命令成功生成 `hello.txt` 文件并写入内容：

```bash
bazel build //generate_files:hello
```

## 创建规则

规则可以在 `.bzl` 文件中使用 `rule` 函数进行定义，并在 `BUILD` 文件中加载调用。通过定义规则，我们可以决定规则能够接受哪些参数以及每个参数的类型限制，并且能够决定如何从输入构建输出。

### rule 函数简介

在定义规则时，我们通常需要指定 `rule` 函数的以下两个参数：

- `implementation`：描述规则如何执行的函数，函数中需要定义从输入构建出输出的 actions。对于规则的每一个实例，在 analysis 阶段会调用 `implementation` 函数。
- `attrs`：声明规则接受参数以及每个参数的类型、限制的字典。例如一个规则通常需要的 `srcs``deps` 等参数可以在这里声明。一些特殊的属性如 `name``visibility` 等会自动添加并且不能进行修改。

关于 `rule` 函数完整介绍可参考 [官方文档](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/skylark/lib/globals.html%23rule)。下面通过一个示例具体说明自定义规则的定义与使用。

### rule 示例

这个示例将定义一个规则用以通过模板创建文件，示例参考自 [Bazel 官方文档](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/skylark/rules-tutorial.html%23create-a-file-with-a-template)，也可以在 GitHub 上[获取](https://link.zhihu.com/?target=https%3A//github.com/bazelbuild/examples)更多规则相关的示例。

示例的目录结构如下：

```text
rule-example
├── template
│   ├── BUILD
│   ├── hello.bzl
│   └── hello.cc.tpl
└── WORKSPACE
```

首先创建模板文件 `template/hello.cc.tpl`：

```cpp
#include <iostream>

int main()
{
    std::cout << "Hello, {NAME}!" << std::endl;
}
```

注意这里的 `{NAME}` 是作为需要替换的字符串，我们也可以定义更多需要替换的部分。

在 `template/hello.bzl` 文件中定义规则以及其 `implementation` 函数：

```python
def _hello_world_impl(ctx):
    out = ctx.actions.declare_file(ctx.label.name + ".cc")
    ctx.actions.expand_template(
        output = out,
        template = ctx.file.template,
        substitutions = {"{NAME}": ctx.attr.username},
    )
    return [DefaultInfo(files = depset([out]))]

hello_world = rule(
    implementation = _hello_world_impl,
    attrs = {
        "username": attr.string(default = "unknown person"),
        "template": attr.label(
            allow_single_file = [".cc.tpl"],
            mandatory = True,
        ),
    },
)
```

`implementation` 函数习惯上以 `_impl` 为后缀进行命名，并以 `_` 开头将其定义为私有函数。函数接收一个参数习惯上命名为 `ctx`，通过 `ctx` 我们可以进行获取 `attrs`，访问文件，定义 actions，声明输出等操作，后文中将会对此简单介绍。这里通过 `ctx.actions.declare_file` 声明了规则的输出文件，通过 `ctx.actions.expand_template` 定义了一个行动。

`implementation` 函数需要返回一个 [Provider](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/skylark/rules.html%23providers)，能够为依赖该规则的规则提供信息，例如这里将规则的输出文件等信息提供给依赖规则。

`attrs` 参数是一个字典，每一项的格式通常为 `属性名: 类型(说明及限制)`，这里的类型可以是 `bool`, `int`, `label`, `string` 等，用 `default` 参数可以规定默认值。完整的类型以及参数说明可见[官方文档](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/skylark/lib/attr.html)。

在 `template/BUILD` 文件中实例化规则并将生成的文件编译成最终的可执行目标文件：

```python
load("hello.bzl", "hello_world")

hello_world(
    name = "hello_Alice",
    username = "Alice",
    template = "hello.cc.tpl",
)

cc_binary(
    name = "hello",
    srcs = [":hello_Alice"],
)
```

需要注意我们没有在 `attrs` 参数中说明 `name` 属性，但是在实例化规则的时候仍然必需指定 `name`。`name` 作为一个特殊的属性会被自动添加到属性列表中。

运行以下命令构建目标：

```bash
bazel build //template:hello
```

### ctx 参数简介

`ctx` 参数是一个上下文对象，通过 `ctx` 可以获取当前目标的标签、属性等参数，同时也能够声明规则的输出文件以及得到输出的行动。下面对 `ctx` 的常用功能进行介绍，其完整介绍可见[官方文档](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/skylark/lib/ctx.html)。

- `ctx.actions`：定义从输入得到输出的 `actions`，例如使用 `ctx.actions.run` 执行程序，使用 `ctx.actions.write` 写文件等，详细可见[官方文档](https://link.zhihu.com/?target=https%3A//docs.bazel.build/versions/main/skylark/lib/actions.html)。
- `ctx.attr`：获取属性，例如定义规则时在 `attrs` 参数中说明了 `username` 属性，则可以使用 `ctx.attr.username` 获取。
- `ctx.file`：对于标签类型的属性，并且标记了 `allow_single_file`，则可以获得标签对应的文件；否则使用 `ctx.files` 来获取文件。

本节讨论了宏与规则，以及使用 `genrule` 定义宏、使用 `rule` 定义规则的方法。[下一节](https://zhuanlan.zhihu.com/p/430975467)将讨论在编译时如何根据不同的用户配置构建出对应的目标结果。